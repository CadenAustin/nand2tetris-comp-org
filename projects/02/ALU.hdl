// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // ZX
    Mux16(a=x, sel=zx, out=zxCond);
    
    // NX
    Not16(in=zxCond, out=notX);
    Mux16(a=zxCond, b=notX, sel=nx, out=nxCond);

    // ZY
    Mux16(a=y, sel=zy, out=zyCond);

    // NY
    Not16(in=zyCond, out=notY);
    Mux16(a=zyCond, b=notY, sel=ny, out=nyCond);

    // F
    Add16(a=nxCond, b=nyCond, out=addOut);
    And16(a=nxCond, b=nyCond, out=andOut);
    Mux16(a=andOut, b=addOut, sel=f, out=baseOut);

    // NO
    Not16(in=baseOut, out=negOut);
    Mux16(a=baseOut, b=negOut, sel=no, out=out, out[15]=negBit, out[0..7]=first8, out[8..15]=second8);

    // ZR
    Or8Way(in=first8, out=exists1);
    Or8Way(in=second8, out=exists2);
    Or(a=exists1, b=exists2, out=exists);
    Not(in=exists, out=zr);

    // NG
    And(a=negBit, b=true, out=ng);
}

